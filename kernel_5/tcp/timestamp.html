<html>
<head>
<title>COLDFORTH KERNEL</title>
<META NAME="Author" CONTENT="Charles Esson">
<META NAME="keywords" CONTENT="forth"> 
<META NAME="description" CONTENT=" COLDFORTH uses this file to generate COLDFORTH">
<LINK REL=stylesheet HREF="../forth.css" TYPE="text/css">
</head>
<body>
<h3>timestamp</h3>
<a HREF="./license.html">license</a><br>

<p>
A user opens a <i>timestamp</i> to use the icmp timestamp protocol. The first two bytes
written are the sequence number. They may be very relevent to the ping word, but they
are of no interest to the system. If you use it as a two byte ascii number you get 99
id's more than enough.
</p>

<p>
Unfortunatly there is a os called NT that doesn't support the
icmp timestamp message. This will only work with Unix boxes. I have a suspect
that the further we go down this path the more useless we will find NT.
</p>

<pre> <code>
io_protocol_common class

		cell% instance_variable %%identifier


		\ errors/stats
		cell% instance_variable %%read_errors
		cell% instance_variable %%bad_timestamp_structure

		m: ( parent--)
			this [parent] :construct
			zero %%bad_timestamp_structure !
			zero %%read_errors !
		; overrides :construct

		\ while the port is open we can recieve more peps
		\ this has to stop before we can proceed.
		| : _wait_for_timestamp_close ( identifier --)
			BEGIN
				DUP _timestamp_find DUP not IF
					\ port addr(--
					2DROP
					EXIT
				THEN
				\ port addr(--
				 _#demux_bucket_object + @ this <> IF
					\ someone else has the port now
					\ port(--
					DROP
					EXIT
				THEN
				\ port(--
				xpause
			AGAIN
		;

		m: ( --)
			\ Remove ourselves from the icmp_timestamp_demux table.
			\ We cannot proceed until this is done. If there is a bug
			\ in the code ( more finds than releases,) we will wait here
			\ forever.
			%%identifier @ _timestamp_remove
			%%identifier @ _wait_for_timestamp_close
			this [parent] :destruct
		; overrides :destruct

		\ string can be /destination_machine
		\               or nothing
		m: ( addr n mode -- instance )
			%%mode !
			\ /machine
			[CHAR] / remove_leading
			\ addr_machine count_machine(--
			DUP IF \ machine specified
				Sresolve_address %%remote_machine !
			ELSE
				2DROP
			THEN
			\ ask the os for a identifier
			\ as soon as we do this we can start receiving
			\ datagrams destined to the allocated port.
			\ As no one knows about us it is not likely, but it
			\ is possible.
			\ getting the identifer adds it to the demux table
			this _timestamp_identifier %%identifier !
			this
		; overrides :file_open


		\ protocol says there is a two byte sequence number after
		\ identifer. As far as we are concerned it is part of the
		\ users data area. How the user uses it is his problem. We
		\ are defintily not going to get involved in creating and
		\ tranfering a sequence number.
		| : setup_timestamp { ( identifier ) variable %pep -- }{
			ic1_length
			DUP CONSTANT _#org_timestamp 4 +
			DUP CONSTANT _#rev_timestamp 4 +
			DUP CONSTANT _#trans_timestamp 4 +
			DUP CONSTANT _#message_length
			DROP
			}

			_%icmp_object @ %pep @ ep_proto_obj + !

			IPT_ICMP %pep @ [ ep_data ip_proto + ]T LITERAL + B!

			%pep @ ep_ipdata + @ ic_seq ERASE

			ICT_TIMERQ	  %pep @ ep_ipdata + @ ic_type + B!
			\ identifier(--
			 %pep @ ep_ipdata + @ ic_id + W!
			ic_seq %pep @ add_to_ip
		;

		m: { variable %pep -- }
			%%local_machine @
			%%remote_machine @
			zero
			%pep @ setup_ip
			\ priority can be set as a object variable.
			\ If you ping a COLDFORTH system it has some meaning.
			\ The rest of the internet expects the priority to be zero
			\ in all ICMP messages.
			%%priority @  5 RSHIFT
			%pep @ [ ep_data ip_tos + ]T LITERAL + B!
			\ ------------------------------------------
			\ this word sets up a header with no options
			\ if you want options do it yourself.
			\ ------------------------------------------
			\ setup the timestamp header; this does not include the sequence.
			%%identifier @
			%pep @
			setup_timestamp

		; overrides :setup_protocol


		\ the icmp timestamp message shows a little logic there is not an
		\ additional count
		| : _add_to_timestamp ( n pep)
			add_to_ip
		;

	</code> </pre>
<p>
An application should call <i>:setup_protocol</i> or <i>:setup_protocol_from_pep</i> to
initialise the pep header. It should then alter header if desired.
</p>
	<pre> <code>
		\ as the timestamp server is implemented in icmp_protocol; there is no
		\ need for this method.
		m: ( variable %pep_in variable %pep_out -- )
			TRUE ABORT" :setup_protocol_from_pep not implemenented."
		; overrides :setup_protocol_from_pep


		| : _pep>timestamp_data ( pep -- pep addr n 0 | $)
			DUP ep_ipdata + @
			\ pep addr(--
			OVER ep_ipcount + @
			\ pep addr len(--
			ic_seq -
			SWAP
			ic_seq +
			SWAP
			zero
		;

		\ The big qestion; should We close on an abort or leave it
		\ to the user. The rule is if you take one of these words you can
		\ convert it into a ior type result with a simple catch. That means
		\ you don't close
		m:  { ( --pep addr n ) }
				%%pep_queue xawait
				%%pep_queue dequeue
				\ pep(--
				DUP ep_error + @ not IF
					_pep>timestamp_data ?DUP IF
						1 %%bad_timestamp_structure +!
						$ABORT
					THEN
					\ pep addr n(--
					EXIT
				THEN
				1 %%read_errors +!
				DUP ep_error + @
				SWAP kill_free_buffer
				$ABORT
		; overrides :read_protocol



		\ the read operation treats each datagram
		\ as a record.
		m:  ( addr len -- len)
			%%active_pep @ not IF
				\ do if for the side effects not the
				\ stack items
				this :read_protocol
				%%active_count !
				%%active_pointer !
				%%active_pep !
			THEN
			\ addr len(--
			_read_from_active_pep
			\ len(--
		; overrides :read

		\ write the data pointed to by addr and n to the pep and queue.
		\ we have to write something as the first two bytes are
		\ the sequene number
		CREATE _$default ," 00 hi"
		m: { ( addr n ) variable %pep -- }
			DUP not IF
				2DROP
				_$default
				COUNT
			THEN
			TUCK
			\ n addr n (--
			%pep @ ep_ipdata + @ ic_seq + SWAP MOVE
			\ n(--
			\ have to now fix up ep_len and ip_length
			%pep @ _add_to_timestamp
			%pep @ queue_ip
		; overrides :write_protocol

		\ returns true if data
		m: ( --flag)
			%%active_pep  @ 0=
			%%pep_queue queue_empty? AND not
		; overrides :key?

		m: ( indent --)
			CR DUP SPACES ." icmp_timestamp | " ." Object: " this .h
			CR DUP SPACES ." remote machine: " %%remote_machine @ .h
			CR DUP SPACES ." local machine      : " %%local_machine @ .h
			CR DUP SPACES ." priority           : " %%priority @ .h
			              ." input_pep_queue : " %%pep_queue .h
			CR DUP SPACES ." datagram receives  : " %%datagrams_received @ .h
			              ." discards        : " %%datagram_discards @ .h
			\ indent(--
			CR DUP SPACES ." type buffer        : " %%type_buffer> @ .h
			              ." type count      : " %%type_buffer_count @ .h
			DROP
			send
		; overrides :print

target_also

&drivers
target_definitions

end_class icmp_timestamp

target_previous_definitions
target_previous

	| : _receive_timestamp { ( object  ) variable %buffer> ( --  send_time his_time current_time true | false ) }{
			zero
			DUP CONSTANT _#sequence 2 +
			DUP CONSTANT _#org_timestamp 4 +
			DUP CONSTANT _#rev_timestamp 4 +
			DUP CONSTANT _#trans_timestamp 4 +
			DUP CONSTANT _#message_length
			DROP
			}
		%buffer> @ _#message_length ROT :read
		\ count (--
		_#message_length = IF
			\ ### temp
			%buffer> @ _#org_timestamp + @
			%buffer> @ _#trans_timestamp + @
			@standard_ms
			TRUE
			EXIT
		THEN
		FALSE
	;


	\ flag is true if message received
	| : _get_timestamp { ( object -- false | send_time his_histime current_time true ) }{
			zero
			DUP CONSTANT _#sequence 2 +
			DUP CONSTANT _#org_timestamp 4 +
			DUP CONSTANT _#rev_timestamp 4 +
			DUP CONSTANT _#trans_timestamp 4 +
			DUP CONSTANT _#message_length
			\ create buffer on stack
			bytes %buffer }
			%buffer _#message_length ERASE
			@standard_ms %buffer _#org_timestamp + !
			%buffer _#message_length jump :write
			DUP :flush_file
			xclock+ @ #10sec + >R
			BEGIN
				xpause
				\ object(--
				^C
				DUP :key?
				IF
					r>drop
					\ object(--
					%buffer _receive_timestamp
					\ false | send_time remote_time current_time true(--
					EXIT
				THEN
				R@ xclock- @ +
				0> not
			UNTIL
			r>drop
			DROP
			FALSE
	;

        \ <a name="$@remote_ticks"></a>
	: $@remote_ticks { ( $  -- remote_ticks TRUE | FALSE ) }{
		$80000000 CONSTANT _#non_standard
		$7FFFFFFF CONSTANT _#time
		#10 CONSTANT _#ms/ticks
		variable %handle
		}
		$" icmp_timestamp/" SWAP  #$buffer get_buffer buffer #$buffer $+

		buffer
		zero $open
		kill_buffer
		%handle !

		%handle @
		\ handle (--
		['] _get_timestamp CATCH IF
			DROP
			%handle @ close
			FALSE
			EXIT
		THEN
		%handle @ close
		IF
			\ success
			\ send_time remote_time recieve_time(--
			OVER _#non_standard AND IF
				2DROP DROP FALSE
				EXIT
			THEN
			_#time AND
			ROT _#time AND - 2/
			SWAP +
			_#ms/ticks /
			TRUE
			EXIT
		THEN
		FALSE
	;
        \ <a name="@remote_ticks"></a>
	: @remote_ticks  ( n -- false | ticks true )
		\ convert n back to a string
		S>D
		#$buffer get_buffer
			buffer #$buffer (d.)
			\ addr n (--
			\ save count at start of string
			SWAP _#$_data - TUCK $count!
			$@remote_ticks
		kill_buffer
	;


        \ <a name="@gateway_ticks"></a>
	: @gateway_ticks ( -- ticks true | false)
		0 0 route_get DUP IF
			rt_gw + @ @remote_ticks
		THEN
	;


	| : init_ticks ( --)
		@gateway_ticks IF
			!ticks
		THEN
	;
	</code> </pre>
</body>
</html>
